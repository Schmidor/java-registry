apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = 11
targetCompatibility = 11

version = '4.5.1'
group = 'at.jta.winregistry'


dependencies {  
  testImplementation 'junit:junit:4.12'
}

def getDate() {
  def date = new Date()
  def formattedDate = date.format('yyyy-MM-dd HH:mm')
  return formattedDate
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
  manifest {
    attributes "Build-Date": getDate()
    attributes "Implementation-Version": version
  }
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }

sourceSets {  
  main {
    java {   
      srcDirs = ['src']
    }
    resources.srcDirs = ['src']
  }
  
  test {
    java {   
      srcDirs = ['test']
    }
    resources.srcDirs = ['test']
  }
}

publishing {
  repositories {
    maven {
      name = "OwnNexus"
  	  url = 'http://maven:8081/nexus/content/repositories/JavaLib/'
    }
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/Schmidor/java-registry")
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
        password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
      }
    }
  }
  publications {
	maven(MavenPublication) {
	  artifactId = 'winregistry'
	  from components.java
	}
  }
}